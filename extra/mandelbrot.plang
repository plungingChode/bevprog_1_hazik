** beallitasok --> lepesszam: 100.000
** alap parameterekkel kb. 92.000 lepes

PROGRAM p32
   VALTOZOK:
      c, x, xn: VALOS[2],
      abs, re, z, y: VALOS,
      i: EGESZ,
      inf: LOGIKAI

   y := 1
   CIKLUS AMIG (y >= -1)

      z := -2
      CIKLUS AMIG (z <= 1)
         c[0] := z
         c[1] := y

         x[0] := c[0]
         x[1] := c[1]

         i := 0
         inf := HAMIS
         CIKLUS AMIG (i < 10 ES NEM inf)
            re := x[0]

            **x ^ 2
            x[0] := re ^ 2 - x[1] ^ 2
            x[1] := 2 * re * x[1] 
 
            **x + c
            xn[0] := x[0] + c[0]
            xn[1] := x[1] + c[1]

            **abs
            abs := |(xn[0] ^ 2 - xn[1] ^ 2)| ^ 0.5

            HA (abs >= 2) AKKOR
               inf := IGAZ
            HA_VEGE

            x[0] := xn[0]
            x[1] := xn[1]
            i := i + 1
         CIKLUS_VEGE
         
         HA (inf) AKKOR
            KI: ' '
         KULONBEN
            KI: '*'
         HA_VEGE

         ** adj hozza kevesebbet a felbontas novelesehez
         ** +-0.01 valtoztatas kb. +-10.000 lepes
         z := z + 0.05
      CIKLUS_VEGE

      ** vonj ki kevesebbet a felbontas novelesehez
      ** +-0.01 valtoztatas kb. +-10.000 lepes
      y := y - 0.1

      KI: SV
   CIKLUS_VEGE
PROGRAM_VEGE
